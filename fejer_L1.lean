/-
Copyright (c) 2025 Bingyu Xia. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Bingyu Xia
-/

import Mathlib

/-
# Convergence of FejÃ©r Sums in $L^1$

This file presents a proof of the result that for any Lebesgue integrable function $f$ on the circle ($f \in L^1(S^1)$),
its sequence of FejÃ©r sums, $\sigma_n(f)$, converges to $f$ in the $L^1$ norm.

The proof hinges on representing the FejÃ©r sum as the convolution of the function $f$ with the FejÃ©r kernel $fejerKernel_n$,
i.e., $\sigma_n(f) = f â‹† fejerKernel_n$. Convergence is then established by showing that the family of FejÃ©r kernels $\{fejerKernel_n\}$ constitutes an approximation to the identity.

We will demonstrate the crucial properties of the FejÃ©r kernel that make it a "good kernel":
1. Unit Integral: The total integral of the kernel is always 1.
2. Positivity: The kernel is non-negative, a key advantage over the Dirichlet kernel.
3. Concentration of Mass: The kernel's mass becomes increasingly concentrated at the origin as $n \to \infty$.

The final step of the proof uses these properties, along with the continuity of translation in the $L^1$ norm (i.e., $\|\tau_y f - f\|_{L^1} \to 0$ as $y \to 0$).
We express the norm of the difference $\|\sigma_n(f) - f\|_{L^1}$ as an integral involving $f$ and $fejerKernel_n$. By strategically splitting this integral,
we show that the contributions from both a small neighborhood of the origin and its complement can be made arbitrarily small for large $n$, which proves the theorem.
-/

open Real MeasureTheory Filter Finset

-- Define Fejer kernel functions
noncomputable def fejerKernel : â„• â†’ â„ â†’ â„‚ := fun n x => 1 / (n + 1) * âˆ‘ m âˆˆ range (n + 1), âˆ‘ k âˆˆ Icc (-m : â„¤) m, Complex.exp (Complex.I * k * x)

-- Prove that $fejerKernel n$ is smooth for all $n$
lemma contDiff_fejerKernel : âˆ€ n, ContDiff â„ âŠ¤ (fejerKernel n) := by
  intro n; unfold fejerKernel
  apply ContDiff.mul; exact contDiff_const
  apply ContDiff.sum; intros; apply ContDiff.sum
  intros; apply ContDiff.cexp; apply ContDiff.mul
  apply ContDiff.mul; any_goals exact contDiff_const
  have : Complex.ofReal = Complex.ofRealCLM := by
    ext; simp only [Complex.ofRealCLM_apply]
  rw [this]; apply ContinuousLinearMap.contDiff

-- Prove a real closed formula of $fejerKernel n$ for $sin (x / 2) â‰  0$ by induction
lemma fejerKernel_eq_real : âˆ€ n x, sin (x / 2) â‰  0 â†’ fejerKernel n x = 1 / (n + 1) * sin ((n + 1) / 2 * x) ^ 2 / sin (x / 2) ^ 2 := by
  intro n x hx; unfold fejerKernel
  rw [â† mul_div]; congr 1
  have : âˆ€ m : â„•, âˆ‘ k âˆˆ Icc (-m : â„¤) m, Complex.exp (Complex.I * k * x) = sin ((m + 1 / 2) * x) / sin (x / 2) := by
    intro m; induction m with
    | zero =>
      simp [-Complex.ofReal_sin]; norm_cast; field_simp
    | succ m ih =>
      push_cast [-Complex.ofReal_sin]
      rw [neg_add', â† sum_erase_add _ _ (show (m:â„¤)+1 âˆˆ Icc ((-m:â„¤)-1) (m+1) by simp; omega)]
      rw [Icc_erase_right, â† sum_erase_add _ _ (show -(m:â„¤)-1 âˆˆ Ico ((-m:â„¤)-1) (m+1) by simp; omega)]
      rw [Ico_erase_left, Ioo_sub_one_left_eq_Ioc, Ico_add_one_right_eq_Icc]
      rw [ih, add_assoc, mul_assoc, mul_assoc]
      nth_rw 2 [mul_comm]; nth_rw 4 [mul_comm]
      rw [Complex.exp_mul_I, Complex.exp_mul_I]; norm_cast
      rw [â† neg_add', Int.cast_neg, neg_mul, cos_neg, sin_neg, Complex.ofReal_neg]
      rw [neg_mul, â† sub_eq_add_neg]; push_cast [-Complex.ofReal_sin, -Complex.ofReal_cos]
      rw [sub_add_add_cancel]; norm_cast; push_cast
      rw [â† eq_sub_iff_add_eq', div_sub_div_same, sin_sub_sin]
      ring_nf; nth_rw 2 [mul_assoc]; rw [mul_inv_cancelâ‚€, mul_one]
      field_simp; exact hx
  replace this : âˆ€ m âˆˆ range (n + 1), âˆ‘ k âˆˆ Icc (-m : â„¤) m, Complex.exp (Complex.I * k * x) = sin ((m + 1 / 2) * x) / sin (x / 2) := by
    intros; apply this
  rw [sum_congr rfl this, â† sum_div]; norm_cast; push_cast
  rw [â† mul_div_mul_left _ _ hx, mul_sum]
  replace this : âˆ€ i âˆˆ range (n + 1), sin (x / 2) * sin ((i + 1 / 2) * x) = 1 / 2 * (cos (i * x) - cos (((i + 1) : â„•) * x)) := by
    intro i _; push_cast
    rw [one_div_mul_eq_div, eq_div_iff]
    nth_rw 2 3 [show (i:â„) = i+1/2-1/2 by ring]
    set t := (i : â„) + 1 / 2 with ht; clear_value t
    rw [sub_mul, show t-1/2+1 = t+1/2 by ring, add_mul, one_div_mul_eq_div]
    rw [cos_add, cos_sub]; ring; norm_num
  rw [sum_congr rfl this, â† mul_sum, sum_range_sub']
  simp [-one_div]; rw [mul_one_sub, one_div_mul_eq_div]
  rw [show (n+1)*x = 2*((n+1)/2*x) by ring, â† sin_sq_eq_half_sub, â† pow_two]

-- Compute the integral of $fejerKernel$ in $[-Ï€, Ï€]$
lemma integral_fejerKernel : âˆ€ n, âˆ« x in (-Ï€)..Ï€, fejerKernel n x = 2 * Ï€ := by
  intro n; unfold fejerKernel
  rw [intervalIntegral.integral_const_mul, intervalIntegral.integral_finset_sum]
  have : âˆ€ x âˆˆ range (n + 1), âˆ« (x_1 : â„) in -Ï€..Ï€, âˆ‘ k âˆˆ Icc (-x : â„¤) x, Complex.exp (Complex.I * k * x_1) = 2 * Ï€ := by
    intro x hx; simp at hx
    rw [intervalIntegral.integral_finset_sum, â† sum_erase_add _ _ (show 0 âˆˆ Icc (-x:â„¤) x by simp)]
    set s := (Icc (-x : â„¤) x).erase 0 with hs; clear_value s
    simp [â† eq_sub_iff_add_eq]; ring_nf; apply sum_eq_zero
    intro i hi; simp [hs] at hi
    have : Set.EqOn (fun (x : â„) => Complex.exp (Complex.I * i * x) ) (fun x => cos (i * x) + Complex.I * sin (i * x)) (Set.uIcc (-Ï€) Ï€) := by
      intro x _; simp; rw [mul_assoc, mul_comm]
      norm_cast; simp [Complex.exp_mul_I]; ring
    rw [intervalIntegral.integral_congr this, intervalIntegral.integral_add]
    rw [intervalIntegral.integral_const_mul, intervalIntegral.integral_ofReal, intervalIntegral.integral_ofReal]
    simp [Complex.ext_iff]; constructor
    Â· rw [intervalIntegral.integral_comp_mul_left]; simp
      norm_cast; exact hi.left
    Â· rw [intervalIntegral.integral_comp_mul_left]; simp
      norm_cast; exact hi.left
    any_goals apply Continuous.intervalIntegrable; fun_prop
    Â· intros; apply Continuous.intervalIntegrable; fun_prop
  simp [sum_congr rfl this]; rw [â† mul_assoc, inv_mul_cancelâ‚€, one_mul]
  Â· norm_cast
  Â· intro i hi; apply Continuous.intervalIntegrable; fun_prop

-- Prove an auxillary fact that $sin (x / 2) = 0$ if and only if $x = 0$ when $x$ belongs to $[-Ï€, Ï€]$
lemma aux_sin : âˆ€ x âˆˆ Set.Icc (-Ï€) Ï€, sin (x / 2) = 0 â†” x = 0 := by
  intro x hx; simp at hx
  rw [sin_eq_zero_iff]; constructor
  Â· rintro âŸ¨n, hnâŸ©; rw [eq_div_iff] at hn; rw [â† hn] at hx
    rcases hx with âŸ¨hx1, hx2âŸ©
    nth_rw 2 [mul_comm] at hx1 hx2; rw [mul_assoc] at hx1 hx2
    rw [â† mul_neg_one, mul_le_mul_left] at hx1; norm_cast at hx1
    nth_rw 2 [â† mul_one Ï€] at hx2; rw [mul_le_mul_left] at hx2
    norm_cast at hx2; simp at hx1; simp [â† hn]
    left; omega
    all_goals positivity
  intro; use 0; symm; simp; assumption

-- Prove that the integral of the norm of $fejerKernel$ in $[-Ï€, Ï€]$ is $2 * Ï€$
lemma integral_norm_fejerKernel : âˆ€ n, âˆ« x in (-Ï€)..Ï€, â€–fejerKernel n xâ€– = 2 * Ï€ := by
  intro n; calc
  _ = âˆ« x in (-Ï€)..Ï€, 1 / (n + 1) * sin ((n + 1) / 2 * x) ^ 2 / sin (x / 2) ^ 2 := by
    apply intervalIntegral.integral_congr_ae
    rw [ae_iff, show (0:ENNReal) = volume {(0:â„)} by simp]; congr 1
    simp only [Classical.not_imp, Set.ext_iff, Set.mem_setOf_eq, Set.mem_singleton_iff]
    intro x; rw [Set.uIoc_eq_union]; nth_rw 2 [Set.Ioc_eq_empty]
    rw [Set.union_empty]; constructor
    Â· rintro âŸ¨xmem, hxâŸ©; revert hx; contrapose!
      intro hx; rw [fejerKernel_eq_real, â† mul_div, norm_mul, norm_div]
      norm_cast; push_cast [Real.norm_eq_abs]
      rw [abs_eq_self.mpr, mul_div]; positivity
      Â· intro h; rw [aux_sin] at h; contradiction
        apply Set.mem_Icc_of_Ioc; exact xmem
    intro hx; unfold fejerKernel; simp [hx]
    split_ands; any_goals norm_cast
    any_goals positivity
    linarith only [pi_pos]
  _ = _ := by
    rw [â† Complex.ofReal_inj, intervalIntegral]; push_cast
    rw [â† integral_fejerKernel n]; simp only [â† integral_complex_ofReal]
    rw [â† intervalIntegral]; apply intervalIntegral.integral_congr_ae
    rw [ae_iff, show (0:ENNReal) = volume {(0:â„)} by simp]; congr 1
    simp only [Classical.not_imp, Set.ext_iff, Set.mem_setOf_eq, Set.mem_singleton_iff]
    intro x; rw [Set.uIoc_eq_union]; nth_rw 2 [Set.Ioc_eq_empty]
    rw [Set.union_empty]; constructor
    Â· rintro âŸ¨xmem, hxâŸ©; revert hx; contrapose!
      intro hx; rw [fejerKernel_eq_real]; push_cast; rfl
      Â· intro h; rw [aux_sin] at h; contradiction
        apply Set.mem_Icc_of_Ioc; exact xmem
    intro hx; unfold fejerKernel; simp [hx]
    split_ands; any_goals norm_cast
    any_goals positivity
    linarith only [pi_pos]

-- Prove the concentration of mass property of the integral of $fejerKernel n$
lemma fejerKernel_mass : âˆ€ Î´ > 0, Î´ < Ï€ â†’ Tendsto (fun n => âˆ« x in (setOf fun x => Î´ â‰¤ |x| âˆ§ |x| â‰¤ Ï€), fejerKernel n x) atTop (nhds 0) := by
  intro Î´ Î´pos Î´lt; rw [Metric.tendsto_atTop]
  have ne_top : volume (Set.Icc (-Ï€) Ï€) â‰  âŠ¤ := by simp
  have aux_sbst : Set.Ioo (-Î´) Î´ âŠ† Set.Icc (-Ï€) Ï€ := by
    simp [Set.subset_def]
    intros; exact âŸ¨by linarith, by linarithâŸ©
  intro Îµ Îµpos; simp
  set S := setOf fun x => Î´ â‰¤ |x| âˆ§ |x| â‰¤ Ï€ with hS; clear_value S
  have S_eq : S = Set.Icc (-Ï€) Ï€ \ Set.Ioo (-Î´) Î´ := by
    simp only [hS, Set.ext_iff, Set.mem_setOf_eq, Set.mem_diff, Set.mem_Icc, Set.mem_Ioo, not_and, not_lt]
    intro x; rw [and_comm, abs_le, le_abs]
    simp only [and_congr_right_iff, and_imp]
    intros; rw [le_neg, Classical.or_iff_not_imp_right, not_le]
  have S_vol : volume S â‰  âŠ¤ := by
    rw [S_eq, measure_diff]; any_goals simp
    Â· exact aux_sbst
  have S_mea : MeasurableSet S := by
    rw [S_eq]; apply MeasurableSet.diff
    exact measurableSet_Icc; exact measurableSet_Ioo
  have aux_ge : âˆ€ x âˆˆ S, sin (Î´ / 2) ^ 2 â‰¤ sin (x / 2) ^ 2 := by
    intro x hx; simp [hS] at hx
    rw [sin_sq_eq_half_sub, sin_sq_eq_half_sub]; gcongr
    norm_num [mul_div_cancelâ‚€]; rw [â† cos_abs]; nth_rw 2 [â† cos_abs]
    apply cos_le_cos_of_nonneg_of_le_pi
    positivity; exact hx.right
    Â· rw [abs_eq_self.mpr]; exact hx.left
      positivity
  use âŒŠ2 * Ï€ / (Îµ * sin (Î´ / 2) ^ 2)âŒ‹â‚Š + 1; intro n nge
  have : Set.EqOn (fejerKernel n) (fun x => 1 / (n + 1) * sin ((n + 1) / 2 * x) ^ 2 / sin (x / 2) ^ 2) S := by
    intro x hx; simp [hS] at hx; rw [fejerKernel_eq_real]
    Â· intro h; rw [aux_sin] at h
      simp [h] at hx; linarith only [hx.left, Î´pos]
      Â· rw [Set.mem_Icc, â† abs_le]; exact hx.right
  rw [setIntegral_congr_fun _ this]; simp_rw [â† mul_div]
  rw [integral_const_mul]; norm_cast
  rw [integral_complex_ofReal, norm_mul]
  simp; norm_cast; push_cast; calc
    _ â‰¤ ((n : â„) + 1)â»Â¹ * âˆ« (x : â„) in S, |sin ((n + 1) / 2 * x) ^ 2 / sin (x / 2) ^ 2| := by
      gcongr; apply abs_integral_le_integral_abs
    _ < _ := by
      rw [inv_mul_eq_div, div_lt_iffâ‚€]
      replace this : âˆ€ x âˆˆ S, |sin ((n + 1) / 2 * x) ^ 2 / sin (x / 2) ^ 2| â‰¤ 1 / sin (Î´ / 2) ^ 2 := by
        intro x hx; simp [hS] at hx
        rw [abs_div, abs_pow]; gcongr
        Â· rw [sq_pos_iff]; intro h; rw [aux_sin] at h
          simp [h] at Î´pos
          Â· rw [Set.mem_Icc]; constructor
            Â· apply le_of_lt; rw [gt_iff_lt] at Î´pos
              apply lt_trans _ Î´pos; simp only [Left.neg_neg_iff]
              positivity
            linarith only [Î´lt]
        Â· rw [sq_le_one_iffâ‚€]; apply abs_sin_le_one
          positivity
        Â· rw [abs_sq, sin_sq_eq_half_sub, sin_sq_eq_half_sub]
          field_simp; gcongr; rw [â† cos_abs]
          apply cos_le_cos_of_nonneg_of_le_pi
          positivity; exact hx.right; exact hx.left
      apply lt_of_le_of_lt (setIntegral_mono_on _ _ _ this)
      Â· rify at nge; replace nge := lt_of_lt_of_le (Nat.lt_floor_add_one _) nge
        simp [integral_const, -one_div]; calc
          _ < volume.real (Set.Icc (-Ï€) Ï€) * (1 / sin (Î´ / 2) ^ 2) := by
            gcongr
            Â· simp [sq_pos_iff]; rw [aux_sin]; positivity
              rw [Set.mem_Icc]; constructor
              Â· apply le_of_lt; rw [gt_iff_lt] at Î´pos
                apply lt_trans _ Î´pos; simp only [Left.neg_neg_iff]
                positivity
              linarith only [Î´lt]
            rw [S_eq, measureReal_diff, sub_lt_self_iff]
            simp; exact Î´pos; exact aux_sbst
            exact measurableSet_Ioo
          _ < _ := by
            rw [volume_real_Icc, sub_neg_eq_add, â† two_mul, max_eq_left, mul_one_div]
            rw [â† div_lt_iffâ‚€', div_div]; nth_rw 2 [mul_comm]
            linarith only [nge]
            all_goals positivity
      Â· apply ContinuousOn.integrableOn_compact
        Â· rw [hS]; replace this : setOf fun x => Î´ â‰¤ |x| âˆ§ |x| â‰¤ Ï€ = Set.Icc (-Ï€) (-Î´) âˆª Set.Icc Î´ Ï€ := by
            simp [Set.ext_iff]; intro x; rw [le_abs, abs_le]
            constructor
            Â· rintro âŸ¨h, âŸ¨âŸ©âŸ©; rcases h with h|h
              Â· right; constructor; all_goals assumption
              left; exact âŸ¨by linarith, by linarithâŸ©
            intro h; rcases h with âŸ¨_,_âŸ©|âŸ¨_,_âŸ©
            Â· constructor; right; linarith
              exact âŸ¨by assumption, by linarithâŸ©
            constructor; left; assumption
            exact âŸ¨by linarith, by assumptionâŸ©
          rw [this]; apply IsCompact.union
          all_goals exact isCompact_Icc
        apply ContinuousOn.abs; apply ContinuousOn.div
        any_goals fun_prop
        Â· intro x hx; simp [hS] at hx
          rw [pow_ne_zero_iff, ne_eq, aux_sin]
          intro h; simp [h] at hx
          linarith only [hx.left, Î´pos]
          Â· rw [Set.mem_Icc, â† abs_le]; exact hx.right
          positivity
      Â· apply integrableOn_const; exact S_vol
        simp
      Â· exact S_mea
      positivity
  exact S_mea

-- Define an auxillary notion `cmul_bl` to be the standard real bilinear forms of complex multiplications on complex numbers
@[continuity]
lemma aux_cont : Continuous fun x : â„‚ =>
    ({ toFun := fun y => x * y, map_add' := by intros; ring, map_smul' := by intros; simp; ring, cont := by continuity } : â„‚ â†’L[â„] â„‚) := by
  rw [Metric.continuous_iff]
  simp [dist_eq_norm]; intro x Îµ Îµpos
  use Îµ / 2; constructor; positivity
  intros; calc
    _ â‰¤ Îµ / 2 := by
      rw [ContinuousLinearMap.opNorm_le_iff]; simp
      intro; rw [â† sub_mul, norm_mul]; gcongr; positivity
    _ < _ := by linarith only [Îµpos]

noncomputable def cmul_bl : â„‚ â†’L[â„] â„‚ â†’L[â„] â„‚ := {
  toFun := fun x => {
    toFun := fun y => x * y
    map_add' := by intros; ring
    map_smul' := by intros; simp; ring
  }
  map_add' := by intros; ext; simp; ring
  map_smul' := by intros; ext; simp; ring
}

lemma aux_cmul : âˆ€ x y, cmul_bl x y = x * y := by simp [cmul_bl]

-- Prove two auxillary integrablility results
lemma aux_integrable [Fact (0 < 2 * Ï€)] : âˆ€ n, Integrable (fejerKernel n âˆ˜ Subtype.val âˆ˜ â‡‘(AddCircle.measurableEquivIoc (2 * Ï€) (-Ï€))) volume := by
  intro n; rw [â† Function.comp_assoc, â† (AddCircle.measurableEquivIoc (2 * Ï€) (-Ï€)).measurableEmbedding.integrable_map_iff]
  rw [â† (MeasurableEmbedding.subtype_coe measurableSet_Ioc).integrable_map_iff]
  have : Measure.map Subtype.val (Measure.map (â‡‘(AddCircle.measurableEquivIoc (2 * Ï€) (-Ï€))) volume) =
  volume.restrict (Set.Ioc (-Ï€) (-Ï€ + 2 * Ï€)) := by
    ext s hs; rw [(MeasurableEmbedding.subtype_coe measurableSet_Ioc).map_apply]
    rw [(AddCircle.measurableEquivIoc (2 * Ï€) (-Ï€)).map_apply, Measure.restrict_apply]
    rw [â† (AddCircle.measurePreserving_mk (2 * Ï€) (-Ï€)).measure_preimage, Measure.restrict_apply]
    congr 1; simp [Set.ext_iff, AddCircle.measurableEquivIoc, AddCircle.equivIoc]
    intro x _ _; rw [(toIocMod_eq_self _).mpr]
    Â· rw [Set.mem_Ioc]; constructor
      assumption; linarith
    Â· apply AddCircle.measurable_mk'; apply (AddCircle.measurableEquivIoc (2 * Ï€) (-Ï€)).measurable
      apply (MeasurableEmbedding.subtype_coe measurableSet_Ioc).measurable; exact hs
    Â· apply MeasurableSet.nullMeasurableSet; apply (AddCircle.measurableEquivIoc (2 * Ï€) (-Ï€)).measurable
      apply (MeasurableEmbedding.subtype_coe measurableSet_Ioc).measurable; exact hs
    exact hs
  rw [this, â† IntegrableOn]; apply Continuous.integrableOn_Ioc
  exact (contDiff_fejerKernel n).continuous

lemma aux_convolution_integrable [Fact (0 < 2 * Ï€)] : âˆ€ n, âˆ€ f : AddCircle (2 * Ï€) â†’ â„‚, Integrable f volume â†’
    Integrable (convolution ((fejerKernel n) âˆ˜ (Subtype.val) âˆ˜ (AddCircle.measurableEquivIoc (2 * Ï€) (-Ï€))) f cmul_bl volume) volume := by
  intro n f hf; apply Integrable.integrable_convolution
  Â· exact aux_integrable n
  exact hf

-- Prove two properties of the translation operators on ğ“›Â¹(AddCircle (2 * Ï€) â†’ â„‚), the first one is the fact that the translation operator preserves ğ“›Â¹-norm
lemma norm_translation [Fact (0 < 2 * Ï€)] : âˆ€ f : AddCircle (2 * Ï€) â†’ â„‚, âˆ€ hf : Integrable f volume, âˆ€ y,
    â€–(hf.comp_sub_right y).toL1â€– = â€–hf.toL1â€– := by
  intro f hf y; simp only [L1.norm_of_fun_eq_integral_norm]
  have aux_eq : Measure.map (fun a => a - y) volume = volume := by
    ext s hs; rw [Measure.map_apply]
    Â· simp only [sub_eq_add_neg, measure_preimage_add_right]
    Â· fun_prop
    exact hs
  have aux_mea : AEMeasurable (fun a => a - y) := by fun_prop
  have aux_smea : AEStronglyMeasurable (fun x => â€–f xâ€–) (Measure.map (fun a => a - y) volume) := by
    apply AEStronglyMeasurable.comp_aemeasurable
    Â· apply Continuous.aestronglyMeasurable
      exact continuous_norm
    Â· rw [aux_eq]; exact hf.aemeasurable
  rw [â† integral_map aux_mea aux_smea, aux_eq]

-- Prove that when $y$ goes to zero, the ğ“›Â¹-norm of the translated functions $f(x-y)$ goes to the ğ“›Â¹-norm of $f(x)$
lemma tendsto_translation [Fact (0 < 2 * Ï€)] : âˆ€ f : AddCircle (2 * Ï€) â†’ â„‚, âˆ€ hf : Integrable f volume,
    Tendsto (fun y => (hf.comp_sub_right y).toL1) (nhds 0) (nhds hf.toL1) := by
  intro f hf; rw [Metric.tendsto_nhds_nhds]
  intro Îµ Îµpos; simp only [dist_eq_norm, sub_zero]
  have : Fact ((1 : ENNReal) â‰¤ 1) := âŸ¨by rflâŸ©
  have := ContinuousMap.toLp_denseRange â„‚ (volume : Measure (AddCircle (2 * Ï€))) â„‚ (show 1 â‰  âŠ¤ by simp)
  simp [denseRange_iff_closure_range, Set.ext_iff, Metric.mem_closure_iff, dist_eq_norm] at this
  specialize this hf.toL1 hf.toL1.mem (Îµ / 3) (by positivity)
  rcases this with âŸ¨g, hgâŸ©
  have aux_g : âˆ€ y, Integrable (g âˆ˜ (fun x => x - y)) := by
    intro; apply Continuous.integrable_of_hasCompactSupport
    apply Continuous.comp
    Â· exact g.continuous
    Â· fun_prop
    apply HasCompactSupport.of_compactSpace
  have int_g := aux_g 0
  rw [show (â‡‘g âˆ˜ fun x => x - 0) = â‡‘g by ext; simp] at int_g
  have := g.uniform_continuity
  specialize this (Îµ / (6 * Ï€)) (by positivity); rcases this with âŸ¨Î´, Î´pos, hÎ´âŸ©
  simp only [dist_eq_norm] at hÎ´
  use Î´; constructor; exact Î´pos
  intro y nylt; calc
    _ = â€–Integrable.toL1 (fun x => f (x - y)) (hf.comp_sub_right y) - (aux_g y).toL1 + ((aux_g y).toL1 - int_g.toL1) + (int_g.toL1 - hf.toL1)â€– := by
      rw [sub_add_sub_cancel, sub_add_sub_cancel]
    _ â‰¤ _ := norm_addâ‚ƒ_le
    _ < _ := by
      rw [show Îµ = Îµ / 3 + Îµ / 3 + Îµ / 3 by ring]; gcongr
      Â· rw [â† Integrable.toL1_sub]
        have : ((fun x => f (x - y)) - â‡‘g âˆ˜ fun x => x - y) = fun x => (f - g) (x - y) := by
          ext; simp only [Pi.sub_apply, Function.comp_apply]
        simp only [this]; rw [norm_translation]
        Â· rw [Integrable.toL1_sub]; exact hg; exact hf
          apply Continuous.integrable_of_hasCompactSupport
          exact ContinuousMap.continuous g
          apply HasCompactSupport.of_compactSpace
      Â· rw [â† Integrable.toL1_sub, L1.norm_of_fun_eq_integral_norm]
        simp only [Pi.sub_apply, Function.comp_apply]
        have : Îµ / 3 = âˆ« (a : AddCircle (2 * Ï€)), Îµ / (6 * Ï€) âˆ‚volume := by
          simp only [integral_const, measureReal_def, AddCircle.measure_univ, smul_eq_mul]
          rw [ENNReal.toReal_ofReal]; field_simp; ring
          positivity
        rw [this, â† sub_pos, â† integral_sub, integral_pos_iff_support_of_nonneg]
        Â· suffices : Function.support (fun a => Îµ / (6 * Ï€) - â€–g (a - y) - g aâ€–) = Set.univ
          Â· rw [this, AddCircle.measure_univ]; positivity
          simp [Set.ext_iff]; intro x
          apply ne_of_gt; rw [sub_pos]; apply hÎ´
          rw [sub_right_comm, sub_self, zero_sub, norm_neg]
          exact nylt
        Â· simp [Pi.le_def]; intro x
          apply le_of_lt; apply hÎ´
          rw [sub_right_comm, sub_self, zero_sub, norm_neg]
          exact nylt
        Â· apply Integrable.sub; apply integrable_const
          apply Integrable.norm; apply Integrable.sub
          apply aux_g; exact int_g
        Â· apply integrable_const
        Â· apply Integrable.norm; apply Integrable.sub
          apply aux_g; exact int_g
      Â· rw [norm_sub_rev]; exact hg

-- The main theorem of ğ“›Â¹-convergence of Fejer sum for Lebesgue integrable functions
theorem Fejer_L1 [Fact (0 < 2 * Ï€)] : âˆ€ f : AddCircle (2 * Ï€) â†’ â„‚, âˆ€ hf : Integrable f volume,
    Tendsto (fun n => (aux_convolution_integrable n f hf).toL1) atTop (nhds ((2 * Ï€) â€¢ hf.toL1)) := by
  intro f hf; rw [Metric.tendsto_atTop]
  intro Îµ Îµpos; simp only [dist_eq_norm, â† Integrable.toL1_smul, â† Integrable.toL1_sub]
  simp only [L1.norm_of_fun_eq_integral_norm, Pi.sub_apply, convolution_def, aux_cmul]
  have := tendsto_translation f hf
  rw [Metric.tendsto_nhds_nhds] at this
  specialize this (Îµ / (4 * Ï€)) (by positivity)
  rcases this with âŸ¨Î´, Î´pos, hÎ´âŸ©
  simp only [dist_eq_norm, sub_zero, â† Integrable.toL1_sub] at hÎ´
  simp only [L1.norm_of_fun_eq_integral_norm, Pi.sub_apply] at hÎ´
  have := fejerKernel_mass (Î´ âŠ“ Ï€ / 2) (by positivity) (lt_of_le_of_lt (min_le_right _ _) (by linarith only [pi_pos]))
  simp only [Metric.tendsto_atTop, dist_eq_norm, sub_zero] at this
  specialize this (Îµ / (4 * â€–hf.toL1â€– + 1)) (by positivity)
  rcases this with âŸ¨N, hNâŸ©
  have aux_sbst : Set.Ioo (-(Î´ âŠ“ Ï€ / 2)) (Î´ âŠ“ Ï€ / 2) âŠ† Set.Icc (-Ï€) Ï€ := by
    simp [Set.subset_def]; intros; constructor
    all_goals linarith [min_le_right Î´ (Ï€ / 2)]
  set S := setOf fun x => Î´ âŠ“ Ï€ / 2 â‰¤ |x| âˆ§ |x| â‰¤ Ï€ with hS; clear_value S
  have S_eq : S = Set.Icc (-Ï€) Ï€ \ Set.Ioo (-(Î´ âŠ“ Ï€ / 2)) (Î´ âŠ“ Ï€ / 2) := by
    simp only [hS, Set.ext_iff, Set.mem_setOf_eq, Set.mem_diff, Set.mem_Icc, Set.mem_Ioo, not_and, not_lt]
    intro x; rw [and_comm, abs_le, le_abs]
    simp only [and_congr_right_iff, and_imp]
    intros; rw [le_neg, Classical.or_iff_not_imp_right, not_le]
  have S_cpt : IsCompact S := by
    suffices : S = Set.Icc (-Ï€) (-(min Î´ (Ï€ / 2))) âˆª Set.Icc (min Î´ (Ï€ / 2)) Ï€
    Â· rw [this]; apply IsCompact.union
      all_goals exact isCompact_Icc
    ext x; simp only [hS, Set.mem_setOf, Set.mem_union, Set.mem_Icc, le_abs, abs_le]
    constructor
    Â· rintro âŸ¨h, âŸ¨âŸ©âŸ©; rcases h with h|h
      Â· right; constructor
        all_goals assumption
      left; constructor; assumption; linarith
    intro h; rcases h with âŸ¨_,hâŸ©|âŸ¨h,_âŸ©
    Â· constructor; right; linarith
      constructor; assumption
      apply le_trans h; apply le_of_lt
      apply lt_trans _ pi_pos; rw [neg_lt, neg_zero]
      positivity
    constructor; left; assumption
    constructor
    Â· apply le_trans _ h; apply le_of_lt; calc
        _ < (0 : â„) := by
          rw [neg_lt, neg_zero]; positivity
        _ < _ := by positivity
    assumption
  have aux : IntegrableOn (fun x : â„ => âˆ« (a_1 : AddCircle (2 * Ï€)), â€–f (a_1 - x) - f a_1â€–) (Set.Ioc (-Ï€) (-Ï€ + 2 * Ï€)) volume := by
    rw [integrableOn_iff_comap_subtypeVal]
    have : ((fun x : â„ => âˆ« (a_1 : AddCircle (2 * Ï€)), â€–f (a_1 - x) - f a_1â€–) âˆ˜ @Subtype.val â„ fun x => x âˆˆ Set.Ioc (-Ï€) (-Ï€ + 2 * Ï€)) =
    ((fun u => âˆ« (a_1 : AddCircle (2 * Ï€)), â€–f (a_1 - u) - f a_1â€–) âˆ˜ QuotientAddGroup.mk) âˆ˜ Subtype.val := by
      ext; simp only [Function.comp_apply]
    rw [this, â† (MeasurableEmbedding.subtype_coe _).integrableOn_range_iff_comap, IntegrableOn]
    simp only [Subtype.range_coe_subtype, Set.setOf_mem_eq]
    rw [(AddCircle.measurePreserving_mk (2 * Ï€) (-Ï€)).integrable_comp]
    set F := fun (x, y) => f (x - y) - f x with hF
    simp only [funext_iff, Prod.forall] at hF; simp only [â† hF]
    apply Integrable.integral_norm_prod_right
    dsimp [F]; apply Integrable.sub
    Â· replace this : Integrable (fun _ => (1 : â„‚)) (volume : Measure (AddCircle (2 * Ï€))) := by
        apply integrable_const
      have aux_conv := Integrable.convolution_integrand cmul_bl this hf
      simp [aux_cmul] at aux_conv; exact aux_conv
    Â· replace this : (fun x : AddCircle (2 * Ï€) Ã— AddCircle (2 * Ï€) => f x.1) =
      fun x => f x.1 * (1 : AddCircle (2 * Ï€) â†’ â„‚) x.2 := by
        ext; simp
      rw [this]; apply Integrable.mul_prod; exact hf
      apply integrable_const
    Â· set F := fun (x, y) => â€–f (y - x) - f yâ€– with hF
      simp only [funext_iff, Prod.forall] at hF; simp only [â† hF]
      apply AEStronglyMeasurable.integral_prod_right'
      dsimp [F]; apply AEStronglyMeasurable.norm
      apply AEStronglyMeasurable.sub
      Â· replace this : AEStronglyMeasurable (fun _ => (1 : â„‚)) (volume : Measure (AddCircle (2 * Ï€)))
        := aestronglyMeasurable_const
        have aux_conv := AEStronglyMeasurable.convolution_integrand cmul_bl this hf.aestronglyMeasurable
        simp only [aux_cmul, one_mul] at aux_conv
        apply AEStronglyMeasurable.prod_swap at aux_conv
        exact aux_conv
      exact hf.aestronglyMeasurable.comp_snd
    all_goals exact measurableSet_Ioc
  use N; intro n nge
  specialize hN n nge; calc
    _ â‰¤ âˆ« (a : AddCircle (2 * Ï€)), âˆ« t : AddCircle (2 * Ï€), â€–(fejerKernel n âˆ˜ Subtype.val âˆ˜ â‡‘(AddCircle.measurableEquivIoc (2 * Ï€) (-Ï€))) tâ€– * â€–f (a - t) - f aâ€– := by
      apply integral_mono
      Â· apply Integrable.norm; apply Integrable.sub
        Â· exact aux_convolution_integrable n f hf
        Â· apply Integrable.smul; exact hf
      Â· set F := fun (x, t) => (fejerKernel n âˆ˜ Subtype.val âˆ˜ â‡‘(AddCircle.measurableEquivIoc (2 * Ï€) (-Ï€))) t * (f (x - t) - f x) with hF
        simp only [funext_iff, Prod.forall] at hF
        simp only [â† norm_mul, â† hF]
        apply Integrable.integral_norm_prod_left
        simp only [F, mul_sub]; apply Integrable.sub
        Â· exact Integrable.convolution_integrand cmul_bl (aux_integrable n) hf
        Â· simp_rw [mul_comm]; apply Integrable.mul_prod
          exact hf; exact aux_integrable n
      rw [Pi.le_def]; intro x
      have : (2 * Ï€) â€¢ f x = âˆ« (t : AddCircle (2 * Ï€)), (fejerKernel n âˆ˜ Subtype.val âˆ˜ â‡‘(AddCircle.measurableEquivIoc (2 * Ï€) (-Ï€))) t * f x := by
        simp only [Complex.real_smul, Complex.ofReal_mul, Complex.ofReal_ofNat, â† integral_fejerKernel n, â† intervalIntegral.integral_mul_const]
        rw [â† AddCircle.integral_preimage _ (-Ï€), intervalIntegral]
        nth_rw 2 [Set.Ioc_eq_empty]; simp [show -Ï€+2*Ï€ = Ï€ by ring]
        simp [AddCircle.measurableEquivIoc, AddCircle.equivIoc]
        apply setIntegral_congr_fun; exact measurableSet_Ioc
        Â· intro a ha; simp only [mul_eq_mul_right_iff]; left
          congr 1; symm; rw [toIocMod_eq_self]
          convert ha; ring
        linarith only [pi_pos]
      simp only [this, â† norm_mul]; rw [â† integral_sub]
      simp only [â† mul_sub]; apply norm_integral_le_integral_norm
      Â· rw [â† (AddCircle.measurePreserving_mk (2 * Ï€) (-Ï€)).integrable_comp, â† IntegrableOn]
        set F := (fun t => (fejerKernel n âˆ˜ Subtype.val âˆ˜ â‡‘(AddCircle.measurableEquivIoc (2 * Ï€) (-Ï€))) t * f (x - t)) âˆ˜ QuotientAddGroup.mk
        suffices : Set.EqOn F (fun t => (fejerKernel n t) * f (x - t)) ((Set.Ioc (-Ï€) (-Ï€ + 2 * Ï€)))
        Â· rw [integrableOn_congr_fun this measurableSet_Ioc, â† integrableOn_Icc_iff_integrableOn_Ioc]
          apply IntegrableOn.continuousOn_mul
          Â· apply Continuous.continuousOn
            exact (contDiff_fejerKernel n).continuous
          rw [integrableOn_Icc_iff_integrableOn_Ioc]
          have : Set.EqOn (fun y : â„ => f (x - y)) ((fun y => f (x - y)) âˆ˜ QuotientAddGroup.mk) (Set.Ioc (-Ï€) (-Ï€ + 2 * Ï€)) := by
            intro; simp
          rw [integrableOn_congr_fun this measurableSet_Ioc, IntegrableOn]
          rw [(AddCircle.measurePreserving_mk (2 * Ï€) (-Ï€)).integrable_comp]
          apply hf.comp_sub_left; exact (hf.comp_sub_left x).aestronglyMeasurable
          exact isCompact_Icc
        intro y hy; simp [F, AddCircle.measurableEquivIoc, AddCircle.equivIoc]
        left; rw [(toIocMod_eq_self _).mpr hy]
        Â· apply AEStronglyMeasurable.mul
          Â· apply AEStronglyMeasurable.comp_measurable
            Â· apply Continuous.aestronglyMeasurable
              exact (contDiff_fejerKernel n).continuous
            apply Measurable.comp
            Â· exact measurable_subtype_coe
            exact (AddCircle.measurableEquivIoc (2 * Ï€) (-Ï€)).measurable
          exact (hf.comp_sub_left x).aestronglyMeasurable
      apply Integrable.mul_const; exact aux_integrable n
    _ = âˆ« (a : â„) in Set.Ioc (-Ï€) Ï€, â€–fejerKernel n aâ€– * âˆ« (a_1 : AddCircle (2 * Ï€)), â€–f (a_1 - â†‘a) - f a_1â€– := by
      rw [integral_integral_swap]; simp only [integral_const_mul]
      rw [â† AddCircle.integral_preimage _ (-Ï€)]
      simp [show -Ï€+2*Ï€ = Ï€ by ring, AddCircle.measurableEquivIoc, AddCircle.equivIoc]
      apply setIntegral_congr_fun; exact measurableSet_Ioc
      Â· intro a ha; simp only [mul_eq_mul_right_iff]; left
        congr; rw [toIocMod_eq_self]
        convert ha; ring
      simp only [â† norm_mul, mul_sub]; apply Integrable.norm
      apply Integrable.sub
      Â· exact Integrable.convolution_integrand cmul_bl (aux_integrable n) hf
      Â· simp_rw [mul_comm]; apply Integrable.mul_prod
        exact hf; exact aux_integrable n
    _ < _ := by
      rw [â† integral_Icc_eq_integral_Ioc, â† Set.diff_union_of_subset aux_sbst]
      rw [setIntegral_union, â† S_eq, show Îµ = Îµ / 2 + Îµ / 2 by ring]; gcongr
      Â· calc
          _ â‰¤ âˆ« (x : â„) in S, â€–fejerKernel n xâ€– * (2 * â€–hf.toL1â€–) := by
            apply setIntegral_mono_on
            Â· apply IntegrableOn.continuousOn_mul
              Â· apply ContinuousOn.norm; apply Continuous.continuousOn
                exact (contDiff_fejerKernel n).continuous
              Â· rw [S_eq]; apply IntegrableOn.mono _ Set.diff_subset (by rfl)
                rw [show Set.Icc (-Ï€) Ï€ = Set.Icc (-Ï€) (-Ï€ + 2 * Ï€) by ring_nf]
                rw [integrableOn_Icc_iff_integrableOn_Ioc]; exact aux
              exact S_cpt
            Â· rw [IntegrableOn]; apply Integrable.mul_const
              apply Integrable.norm; rw [â† IntegrableOn]
              apply ContinuousOn.integrableOn_compact S_cpt
              apply Continuous.continuousOn; exact (contDiff_fejerKernel n).continuous
            Â· rw [S_eq]; apply MeasurableSet.diff
              exact measurableSet_Icc; exact measurableSet_Ioo
            Â· intro x hx; gcongr
              suffices : âˆ« (a_1 : AddCircle (2 * Ï€)), â€–f (a_1 - x) - f a_1â€– = â€–(hf.comp_sub_right x).toL1 - hf.toL1â€–
              Â· rw [this, two_mul (â€–Integrable.toL1 f hfâ€–)]
                nth_rw 1 [â† norm_translation f hf x]; apply norm_sub_le
              rw [â† Integrable.toL1_sub, L1.norm_of_fun_eq_integral_norm]
              simp only [Pi.sub_apply]
          _ < _ := by
            rw [integral_mul_const]
            suffices : Set.EqOn (fejerKernel n) (fun x => (1 / (n + 1) * sin ((n + 1) / 2 * x) ^ 2 / sin (x / 2) ^ 2)) S
            Â· rw [setIntegral_congr_fun _ this] at hN
              simp only [â† mul_div, integral_const_mul, norm_mul, norm_div] at hN
              norm_cast at hN; simp only [integral_complex_ofReal, Complex.norm_real] at hN
              replace this : Set.EqOn (fun x => â€–fejerKernel n xâ€–) (fun x => (1 / (n + 1) * sin ((n + 1) / 2 * x) ^ 2 / sin (x / 2) ^ 2)) S := by
                intro y hy; rw [Set.EqOn] at this
                specialize this hy; simp only [this, â† mul_div, â† Complex.ofReal_pow, â† Complex.ofReal_div]
                rw [norm_mul, Complex.norm_real, Real.norm_eq_abs]; congr 1
                Â· rw [norm_div]; norm_cast
                Â· rw [abs_eq_self]; positivity
              rw [setIntegral_congr_fun _ this]
              by_cases h : â€–Integrable.toL1 f hfâ€– = 0
              Â· simp [h]; exact Îµpos
              rw [Real.norm_eq_abs, abs_eq_self.mpr] at hN; push_cast at hN
              rw [â† lt_div_iffâ‚€, div_div]; simp only [â† mul_div, integral_const_mul]
              apply lt_trans hN; gcongr
              rw [â† sub_pos]; ring_nf; any_goals positivity
              all_goals rw [S_eq]; apply MeasurableSet.diff
              any_goals exact measurableSet_Icc
              all_goals exact measurableSet_Ioo
            intro y hy; rw [fejerKernel_eq_real]; intro h
            rw [aux_sin] at h; simp only [hS, Set.mem_setOf, h, abs_zero] at hy
            replace hy := hy.left; revert hy
            rw [imp_false, not_le]; positivity
            Â· rw [S_eq] at hy; apply Set.diff_subset at hy
              exact hy
      calc
        _ < âˆ« (x : â„) in Set.Ioo (-min Î´ (Ï€ / 2)) (min Î´ (Ï€ / 2)), â€–fejerKernel n xâ€– * (Îµ / (4 * Ï€)) := by
          rw [â† sub_pos, â† integral_sub, setIntegral_pos_iff_support_of_nonneg_ae]
          Â· rw [Set.inter_comm, measure_inter_conull', volume_Ioo, ENNReal.ofReal_pos]
            ring_nf; positivity
            Â· suffices : volume (setOf fun x => |x| < Î´ âŠ“ (Ï€ / 2) âˆ§ fejerKernel n x = 0) = 0
              Â· rw [â† this]; congr 1
                simp only [Set.ext_iff, Set.mem_diff, Set.mem_Ioo, â† abs_lt, Function.notMem_support, Set.mem_setOf]
                simp only [and_congr_right_iff]; intro x hx
                rw [â† mul_sub, mul_eq_zero_iff_right]; exact norm_eq_zero
                Â· apply ne_of_gt; rw [sub_pos]; apply hÎ´
                  rw [(AddCircle.norm_coe_eq_abs_iff (2 * Ï€) (by positivity)).mpr]
                  apply lt_of_lt_of_le hx; apply min_le_left
                  Â· nth_rw 2 [abs_eq_self.mpr]; norm_num
                    apply le_of_lt; apply lt_trans hx
                    apply lt_of_le_of_lt (min_le_right _ _)
                    linarith only [pi_pos]; positivity
              let F : â„¤ â†’ â„ := fun k => 2 * k / (n + 1) * Ï€
              have aux_sbst : {x | |x| < min Î´ (Ï€ / 2) âˆ§ fejerKernel n x = 0} âŠ† F '' (setOf fun x => |x| â‰¤ âŒŠ(n + 1) * (Î´ âŠ“ (Ï€ / 2)) / (2 * Ï€)âŒ‹) := by
                simp only [Set.subset_def, Set.mem_setOf, Set.mem_image, F]
                rintro x âŸ¨hx1, hx2âŸ©; have : x â‰  0 := by
                  intro h; revert hx2; rw [imp_false]
                  unfold fejerKernel; simp [h]; constructor
                  all_goals norm_cast
                  positivity
                rw [fejerKernel_eq_real, â† mul_div, mul_eq_zero_iff_left] at hx2; norm_cast at hx2
                rw [div_eq_zero_iff, or_comm] at hx2; rcases hx2 with hx2|hx2
                Â· rw [sq_eq_zero_iff, aux_sin] at hx2; contradiction
                  rw [Set.mem_Icc, â† abs_le]; apply le_of_lt
                  apply lt_trans hx1; apply lt_of_le_of_lt (min_le_right _ _)
                  linarith only [pi_pos]
                push_cast [sq_eq_zero_iff, sin_eq_zero_iff] at hx2
                rcases hx2 with âŸ¨m, hmâŸ©; symm at hm
                rw [mul_comm, â† eq_div_iff, div_div_eq_mul_div] at hm
                use m; constructor
                Â· rw [Int.le_floor, Int.cast_abs]
                  simp only [hm, â† mul_div, abs_mul, abs_div] at hx1
                  norm_cast at hx1; push_cast at hx1
                  nth_rw 2 [abs_eq_self.mpr] at hx1
                  rw [mul_assoc, â† lt_div_iffâ‚€, mul_div, div_div_eq_mul_div] at hx1
                  rw [mul_comm] at hx1; nth_rw 2 [mul_comm] at hx1
                  exact le_of_lt hx1; all_goals positivity
                rw [hm]; ring; positivity
                Â· simp only [one_div, ne_eq, inv_eq_zero]
                  norm_cast
                revert this; contrapose!
                rw [aux_sin]; exact fun a => a
                Â· rw [Set.mem_Icc, â† abs_le]; apply le_of_lt
                  apply lt_trans hx1; apply lt_of_le_of_lt (min_le_right _ _)
                  linarith only [pi_pos]
              apply measure_mono at aux_sbst; rw [eq_iff_le_not_lt]; constructor
              Â· convert aux_sbst; symm; rw [Set.image_eq_iUnion]
                apply measure_iUnion_null
                simp only [Set.mem_setOf_eq, measure_iUnion_null_iff, measure_singleton, implies_true]
              push_neg; exact zero_le'
              exact Measure.instOuterMeasureClass
          Â· rw [EventuallyLE]; apply ae_restrict_of_forall_mem measurableSet_Ioo
            intro y hy; simp only [Pi.zero_apply, â† mul_sub]
            rw [Set.mem_Ioo, â† abs_lt] at hy
            apply mul_nonneg; positivity
            Â· rw [sub_nonneg]; apply le_of_lt
              apply hÎ´; rw [(AddCircle.norm_coe_eq_abs_iff (2 * Ï€) (by positivity)).mpr]
              apply lt_of_lt_of_le hy; apply min_le_left
              Â· nth_rw 2 [abs_eq_self.mpr]; norm_num
                apply le_of_lt; apply lt_trans hy
                apply lt_of_le_of_lt (min_le_right _ _)
                linarith only [pi_pos]; positivity
          Â· simp only [â† mul_sub]; rw [â† integrableOn_Icc_iff_integrableOn_Ioo]
            apply IntegrableOn.continuousOn_mul
            Â· apply Continuous.continuousOn; apply Continuous.norm
              exact (contDiff_fejerKernel n).continuous
            Â· rw [IntegrableOn]; apply Integrable.sub
              Â· apply integrable_const
              rw [â† IntegrableOn]; apply aux.mono
              simp only [Set.subset_def, Set.mem_Icc, â† abs_le]
              intro x hx; ring_nf; apply Set.mem_Ioc_of_Ioo
              rw [Set.mem_Ioo, â† abs_lt]; apply lt_of_le_of_lt hx
              apply lt_of_le_of_lt (min_le_right _ _)
              linarith only [pi_pos]; rfl
            exact isCompact_Icc
          Â· apply Integrable.mul_const; apply Integrable.norm
            rw [â† IntegrableOn, â† integrableOn_Icc_iff_integrableOn_Ioo]
            apply Continuous.integrableOn_Icc
            exact (contDiff_fejerKernel n).continuous
          Â· rw [â† IntegrableOn, â† integrableOn_Icc_iff_integrableOn_Ioo]
            apply IntegrableOn.continuousOn_mul
            Â· apply Continuous.continuousOn; apply Continuous.norm
              exact (contDiff_fejerKernel n).continuous
            Â· apply aux.mono; simp only [Set.subset_def, Set.mem_Icc, â† abs_le]
              intro x hx; ring_nf; apply Set.mem_Ioc_of_Ioo
              rw [Set.mem_Ioo, â† abs_lt]; apply lt_of_le_of_lt hx
              apply lt_of_le_of_lt (min_le_right _ _)
              linarith only [pi_pos]; rfl
            exact isCompact_Icc
        _ â‰¤ âˆ« (x : â„) in (-Ï€)..Ï€, â€–fejerKernel n xâ€– * (Îµ / (4 * Ï€)) := by
          rw [intervalIntegral]; nth_rw 2 [Set.Ioc_eq_empty]
          simp only [Measure.restrict_empty, integral_zero_measure, sub_zero]
          apply setIntegral_mono_set
          Â· rw [â† integrableOn_Icc_iff_integrableOn_Ioc]
            apply Continuous.integrableOn_Icc; apply Continuous.mul
            apply Continuous.norm; exact (contDiff_fejerKernel n).continuous
            exact continuous_const
          Â· rw [EventuallyLE]; apply ae_of_all
            intro; rw [Pi.zero_apply]; positivity
          Â· rw [ae_le_set]
            suffices : (Set.Ioo (-min Î´ (Ï€ / 2)) (min Î´ (Ï€ / 2)) \ Set.Ioc (-Ï€) Ï€) = âˆ…
            Â· simp [this]
            rw [Set.diff_eq_empty, Set.subset_def]; intro x hx
            apply Set.mem_Ioc_of_Ioo; rw [Set.mem_Ioo, â† abs_lt] at *
            apply lt_trans hx; apply lt_of_le_of_lt (min_le_right _ _)
            linarith only [pi_pos]
          linarith only [pi_pos]
        _ = _ := by
          rw [intervalIntegral.integral_mul_const, integral_norm_fejerKernel]
          field_simp; ring
      apply Set.disjoint_sdiff_left; exact measurableSet_Ioo
      Â· apply IntegrableOn.continuousOn_mul
        Â· apply Continuous.continuousOn; apply Continuous.norm
          exact (contDiff_fejerKernel n).continuous
        Â· rw [â† integrableOn_Icc_iff_integrableOn_Ioc, show -Ï€ + 2 * Ï€ = Ï€ by ring] at aux
          apply aux.mono; apply Set.diff_subset; rfl
        rw [â† S_eq]; exact S_cpt
      Â· rw [â† integrableOn_Icc_iff_integrableOn_Ioo]
        apply IntegrableOn.continuousOn_mul
        Â· apply Continuous.continuousOn; apply Continuous.norm
          exact (contDiff_fejerKernel n).continuous
        Â· apply aux.mono; simp only [Set.subset_def, Set.mem_Icc, â† abs_le]
          intro x hx; apply Set.mem_Ioc_of_Ioo
          ring_nf; rw [Set.mem_Ioo, â† abs_lt]; apply lt_of_le_of_lt hx
          apply lt_of_le_of_lt (min_le_right _ _)
          linarith only [pi_pos]; rfl
        exact isCompact_Icc
